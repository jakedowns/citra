apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

if (!isStandalone()) {
    apply from: getRelativeRepoPath('leia/common/android/gradle/setup.gradle')
}

android {
    namespace "com.leia.lwe.java.sample"

    compileSdkVersion 31

    defaultConfig {
        applicationId = 'com.leia.lwe.java.sample'
        minSdkVersion 30
        targetSdkVersion 30

        versionCode 1
        versionName "0.0.1"

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_static"
                if (isStandalone()) {
                    arguments "-DLWE_STANDALONE=ON"
                    arguments "-DCMAKE_PREFIX_PATH=$project.cnsdkPath"
                    arguments "-DCMAKE_FIND_ROOT_PATH_MODE_PACKAGE=BOTH"
                } else {
                    setDefaultCmakeArguments(arguments)
                    if (useInAppFaceTracking()) {
                        arguments "-DLNK_IN_APP_TRACKING=ON"
                        arguments "-DLNK_IN_SERVICE_TRACKING=OFF"
                    } else {
                        arguments "-DLNK_IN_APP_TRACKING=OFF"
                        arguments "-DLNK_IN_SERVICE_TRACKING=ON"
                    }
                }
            }
        }
        ndk { abiFilters 'arm64-v8a' }

        sourceSets {
            main {
                assets.srcDirs '../../assets'
            }
        }

        packagingOptions {
            pickFirst '**/libleiaSDK.so'
            pickFirst '**/lib*blink.so'
        }

        if (shouldAddDebugInfo()) {
            packagingOptions {
                doNotStrip '**/*.so'

                jniLibs {
                    keepDebugSymbols += "**/*.so"
                }
            }

            ndk {
                debugSymbolLevel 'FULL'
            }
        }

        packagingOptions {
            jniLibs {
                useLegacyPackaging true
            }
        }

        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            jniDebuggable true
        }
    }

    ndkVersion project.ndkVersion
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version project.cmakeVersion
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.core:core-ktx:1.7.0'
}

addSdkDependency(dependencies, false)

clean {
    delete ".cxx"
}
